<job id="Install-ActiveClient">
<script language="VBScript" src="..\..\scripts\ZTIUtility.vbs"/>
<script language="VBScript">
 
' //----------------------------------------------------------------------------
' //
' // Solution: Richard's Deployment Script
' // File: Install-ActiveClient.wsf
' //
' // Purpose: This will install ActiveClient
' //
' // Author: Richard Tracy
' //
' // Usage: cscript Install-ActiveClient.wsf [/debug:true]
' //
' //----------------------------------------------------------------------------
 
'//----------------------------------------------------------------------------
'//
'// Global constant and variable declarations
'//
'//----------------------------------------------------------------------------
 
Option Explicit 

Dim iRetVal 
'//----------------------------------------------------------------------------
'// End declarations
'//----------------------------------------------------------------------------
 
'//----------------------------------------------------------------------------
'// Main routine
'//----------------------------------------------------------------------------
 
On Error Resume Next
iRetVal = ZTIProcess
ProcessResults iRetVal
On Error Goto 0 

'//---------------------------------------------------------------------------
'//
'// Function: ZTIProcess()
'//---------------------------------------------------------------------------
Function ZTIProcess() 

	Dim sVersion,sArch,sFile
	Dim sInstallName, sInstallerPath
	Dim sPatch, sInstallerPatchPath
	Dim sLogFolder,sLogName,sLogPath
	
	'// Variables
	sVersion = "6.2"
	sArch = "x64"
	sPatch = "FIXS1107012_x64.msp"
	
	If sArch = "x64" then
		sFile = "ActivClient_CAC_x64_"& sVersion &".msi"
	Else
		sFile = "ActivClient_CAC_x86_"& sVersion &".msi"
	End If
	
	sInstallName = "ActivClient"
	
	sInstallerPath = oUtility.ScriptDir & "\source\" & sVersion & "\" & sFile
	sInstallerPatchPath = oUtility.ScriptDir & "\source\" & sVersion & "\" & sPatch
	
	'// Build log path (incase not used in task sequence)
	If oEnvironment.Item("_SMSTSLogPath") = "" Then 
		sLogFolder = oEnv("TEMP") & "\AppLogs"
	Else
		sLogFolder = oEnvironment.Item("_SMSTSLogPath") & "\AppLogs"
	End If
	oUtility.VerifyPathExists sLogPath
	sLogName = Replace(sInstallName & "_" & sVersion & "_" & sArch & ".log"," ","")
	sLogPath = sLogFolder & "\" & sLogName

	'// Start the process
	oLogging.CreateEntry "Starting " & sInstallName & " (" & sVersion & ") " & sArch & " installation", LogTypeInfo	
	
	If not oFSO.FileExists(sInstallerPath) then
		oLogging.CreateEntry sInstallerPath & " was not found, unable to install " & sInstallName & " (" & sVersion & ")", LogTypeError
		ZTIProcess = Failure
		Exit Function
	End if

	'// Disable Zone Checks
	oEnv("SEE_MASK_NOZONECHECKS") = 1 

	iRetVal = oUtility.RunWithHeartbeat("msiexec /i """ & sInstallerPath & """ /qn /norestart /l*v """ & sLogPath & """")
	
	if (iRetVal = 0) or (iRetVal = 3010) then
		ZTIProcess = Success
		oUtility.RunWithHeartbeat("msiexec /p """ & sInstallerPatchPath & """ /qn /norestart")
		oLogging.CreateEntry "Finished " & sInstallName & " installation", LogTypeInfo		
	Else 
		ZTIProcess = Failure
		oLogging.CreateEntry sInstallName & " installation failed with exit code = " & iRetVal, LogTypeError
	End If
	
	'// Enable Zone Checks
	oEnv.Remove("SEE_MASK_NOZONECHECKS")
	
End Function 
</script>
</job>

