<job id="Install-ChromeEE">
<script language="VBScript" src="..\..\scripts\ZTIUtility.vbs"/>
<script language="VBScript">
 
' //----------------------------------------------------------------------------
' //
' // Solution: Richard's Deployment Script
' // File: Install-ChromeEE.wsf
' //
' // Purpose: This will install Google Chrome Enterprise Edition
' //
' // Author: Richard Tracy
' //
' // Usage: cscript Install-ChromeEE.wsf [/arch:x64|x86] [/debug:true]
' //
' //----------------------------------------------------------------------------
 
'//----------------------------------------------------------------------------
'// Global constant and variable declarations
'//----------------------------------------------------------------------------
Option Explicit 

Dim iRetVal 

'//----------------------------------------------------------------------------
'// Main routine
'//----------------------------------------------------------------------------
'On Error Resume Next
iRetVal = ZTIProcess
ProcessResults iRetVal
On Error Goto 0 

'//---------------------------------------------------------------------------
'// Function: ZTIProcess()
'//---------------------------------------------------------------------------
Function ZTIProcess() 

	Dim sVersion,sArch,sFile
	Dim sInstallName, sInstallerPath
	Dim sLogFolder,sLogName,sLogPath
	Dim sPrefix
	Dim sInstalledPath
	
	'// Apply Architecture arguments:
	'// If no argument provided check for MDT/SCCM variable
	'// If no variable or argument is provided, defualt to x86
	If oUtility.Arguments.Exists("arch") Then
		sArch = LCase(oUtility.Arguments("arch"))
	ElseIf oEnvironment.Exists("Architecture") Then
		sArch = LCase(oEnvironment.Item("Architecture"))
	Else
		sArch = "x86"
	End If
	
	'Apply Arguments
	If oUtility.Arguments.Exists("prefix") Then
		sPrefix = oUtility.Arguments("prefix")
	ElseIf oEnvironment.Exists("Prefix") Then
		sPrefix = oEnvironment.Item("Prefix")
	End if
	
	'// Variables: 
	'// Change if needed
	sVersion = "74.0.3729.157"
	
	If sArch = "x64" then
		sFile = "googlechromestandaloneenterprise64.msi"
	Else
		sFile = "googlechromestandaloneenterprise.msi"
	End If
	
	sInstallName = "Google Chrome Enterprise Edition"
	
	sInstallerPath = oUtility.ScriptDir & "\Source\" & sVersion & "\" & sFile
	
	'// Build log path (incase not used in task sequence)
	If oEnvironment.Exists("_SMSTSLogPath") Then 
		sLogFolder = oEnvironment.Item("_SMSTSLogPath") & "\AppLogs"
	Else
		sLogFolder = oEnv("TEMP") & "\AppLogs"
	End If
	oUtility.VerifyPathExists sLogFolder
	sLogName = Replace(sInstallName & "_" & sVersion & "_" & sArch & ".log"," ","")
	sLogPath = sLogFolder & "\" & sLogName

	'// Start the process
	oLogging.CreateEntry "Starting " & sInstallName & " (" & sVersion & ") " & sArch & " installation", LogTypeInfo	
	
	If not oFSO.FileExists(sInstallerPath) then
		oLogging.CreateEntry sInstallerPath & " was not found, unable to install " & sInstallName & " (" & sVersion & ")", LogTypeError
		ZTIProcess = Failure
		Exit Function
	End if

	'// Disable Zone Checks
	oEnv("SEE_MASK_NOZONECHECKS") = 1 

	iRetVal = oUtility.RunWithHeartbeat("msiexec /i """ & sInstallerPath & """ /quiet /norestart /l*v """ & sLogPath & """")
	
	if (iRetVal = 0) or (iRetVal = 3010) then
		ZTIProcess = Success 
		oLogging.CreateEntry "Finished " & sInstallName & " installation", LogTypeInfo 
		If sArch = "x64" then
			sInstalledPath = oEnv("PROGRAMFILES") & "\Google\Chrome\Application"
		Else
			sInstalledPath = oEnv("PROGRAMFILESX86") & "\Google\Chrome\Application"
		End If
		
		oUtility.VerifyPathExists sInstalledPath
		If oFSO.FileExists(oUtility.ScriptDir & "\Configs\" & sPrefix & "\master_preferences") then
			oFSO.CopyFile (oUtility.ScriptDir & "\Configs\" & sPrefix & "\master_preferences"), (sInstalledPath & "\master_preferences"), True
		End if
	Else 
		ZTIProcess = Failure
		oLogging.CreateEntry sInstallName & " installation failed with exit code = " & iRetVal, LogTypeError
	End If
	
	'// Enable Zone Checks
	oEnv.Remove("SEE_MASK_NOZONECHECKS")	

End Function 

</script>
</job>










