<job id="Install-DefenderDef">
<script language="VBScript" src="..\..\scripts\ZTIUtility.vbs"/>
<script language="VBScript"> 

' //***************************************************************************
' // ***** Script Header *****
' //
' // Solution: MDT Solution Pack
' // File: Install-DefenderDef.wsf
' //
' // Purpose: Used to install Microsoft Defender Definitions
' //
' // Usage: cscript Install-DefenderDef.wsf [/debug:true]
' //
' // Script Version: 1.0.0
' // Microsoft History:
' //
' // This script is provided "AS IS" with no warranties, confers no rights and 
' // is not supported by the authors or Microsoft Corporation. 
' // Use of this script sample is subject to the terms specified at 
' // http://www.microsoft.com/info/cpyright.mspx.
' //
' // ***** End Header *****
' //***************************************************************************

'//----------------------------------------------------------------------------
'//
'// Global constant and variable declarations
'//
'//---------------------------------------------------------------------------- 

Option Explicit 

Dim iRetVal 

'//----------------------------------------------------------------------------
'// End declarations
'//---------------------------------------------------------------------------- 

'//----------------------------------------------------------------------------
'// Main routine
'//---------------------------------------------------------------------------- 

'On Error Resume Next
iRetVal = ZTIProcess
ProcessResults iRetVal
On Error Goto 0 

'//---------------------------------------------------------------------------
'//
'// Function: ZTIProcess()
'//
'// Input: None
'// 
'// Return: Success - 0
'// Failure - non-zero
'//
'// Purpose: Perform main ZTI processing
'// 
'//---------------------------------------------------------------------------
Function ZTIProcess() 
	iRetVal = Success 
	ZTIProcess = iRetval 
	
	Dim sInstallName
	
	'// Variables
	sInstallName = "Microsoft Defender Definitions"
	
	oLogging.CreateEntry "Starting installation", LogTypeInfo	

	' Look for a file with the pattern *MSEInstall*.exe
	Dim oFolder, files, folderIdx
	Dim sSourceDir, sInstaller,sNISInstaller,sInstallPath
  	Dim oRegExp
  	Set oRegExp = CreateObject("VBScript.RegExp")
  	oRegExp.Pattern = "(.*)mpam-fe(.*)\.exe"
	oRegExp.IgnoreCase = True
	
	if oEnvironment.item("Architecture") = "X64" then  
		sSourceDir = oUtility.ScriptDir & "\Source\Win8_10\x64"
		oLogging.CreateEntry "" & sSourceDir &" path will be used.", LogTypeInfo
	Elseif oEnvironment.item("Architecture") = "X86" then
		sSourceDir = oUtility.ScriptDir & "\Source\Win8_10\x86"
		oLogging.CreateEntry "" & sSourceDir &" path will be used.", LogTypeInfo
	Else
		sSourceDir = oUtility.ScriptDir & "\Source\Win8_10\x64"
		oLogging.CreateEntry "" & sSourceDir &" path will be used by force.", LogTypeInfo
	End if
	
	sInstaller = ""
	Set oFolder = oFSO.GetFolder(sSourceDir)
	Set files = oFolder.Files    
	For Each folderIdx In files
		If oRegExp.Test(folderIdx.Name) Then 
			sInstaller = folderIdx.Name
			Exit For
		End If 
	Next

	If sInstaller = "" Then
		oLogging.CreateEntry "file was not found, unable to install.", LogTypeError
		ZTIProcess = Failure
		Exit Function
	Else
		oLogging.CreateEntry "" & sInstaller & " file will be ran", LogTypeInfo
	End If
	
	'// Disable Zone Checks
	oEnv("SEE_MASK_NOZONECHECKS") = 1 

	oLogging.CreateEntry "Starting Security Essentials definition", LogTypeInfo
	
	sInstallPath = sSourceDir & "\" & sInstaller
	iRetVal = oUtility.RunWithHeartbeat("""" & sInstallPath & """")
	if (iRetVal = 0) or (iRetVal = 3010) then
		ZTIProcess = Success 
		oLogging.CreateEntry "Finished Defender definition file ran", LogTypeInfo	
	Else 
		ZTIProcess = Failure
		oLogging.CreateEntry "Failed to update Defender definitions. Return code from command = " & iRetVal, LogTypeInfo
	End If
	
	
	
	sNISInstaller = oUtility.ScriptDir & "\Source\NIS\x64\nis_full.exe"
	If not oFSO.FileExists(sNISInstaller) then
		oLogging.CreateEntry "" & sNISInstaller & " was not found, unable to install AntiMalware definitions", LogTypeError
		ZTIProcess = Failure
		Exit Function
	End if

	iRetVal = oUtility.RunWithHeartbeat("""" & sNISInstaller & """")
	if (iRetVal = 0) or (iRetVal = 3010) then
		ZTIProcess = Success 
		oLogging.CreateEntry "Finished AntiMalware definition file Install", LogTypeInfo	
	Else 
		ZTIProcess = Failure
		oLogging.CreateEntry "Failed to update AntiMalware definitions. Return code from command = " & iRetVal, LogTypeInfo
	End If
	
	'// Enable Zone Checks
	oEnv.Remove("SEE_MASK_NOZONECHECKS")
	
End Function 

Function GetPatchFileList(FileFolder, FileStringArray()) 

	Dim objFS, objFolder, objFile
	Dim numPatches 
	'create file system object 
	Set objFS=CreateObject("Scripting.FileSystemObject") 
	'set the a variable to point to our folder with the patches in it. 
	Set objFolder=objFS.GetFolder(FileFolder) 
	'set the initial file count to 0 
	numPatches=0 

	for each objFile in objFolder.Files 
		if UCase(Right(objFile.Name,4))=".EXE" then 
			numPatches=numPatches+1 

			'redim preserve FileStringArray(numPatches) 
			FileStringArray(numPatches)=objFile.Name 
		end if 

	next 

	GetPatchFileList=numPatches 
End Function 


</script>
</job>















