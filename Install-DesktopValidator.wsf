<job id="Install-DesktopValidator">
<script language="VBScript" src="..\..\scripts\ZTIUtility.vbs"/>
<script language="VBScript"> 

' //----------------------------------------------------------------------------
' //
' // Solution: Richard's Deployment Script
' // File: Install-DesktopValidator.wsf
' //
' // Purpose: This will install Tumbleweed Desktop Validator
' //
' // Author: Richard Tracy
' //
' // Usage: cscript Install-Command.wsf [/config:<filepath>] [/arch:x64|x86] [/debug:true]
' //
' //----------------------------------------------------------------------------

'//----------------------------------------------------------------------------
'// Global constant and variable declarations
'//---------------------------------------------------------------------------- 
Option Explicit 

Dim iRetVal

'//----------------------------------------------------------------------------
'// Main routine
'//---------------------------------------------------------------------------- 
'On Error Resume Next
iRetVal = ZTIProcess
ProcessResults iRetVal
On Error Goto 0

'//---------------------------------------------------------------------------
'// Function: ZTIProcess()
'//---------------------------------------------------------------------------
Function ZTIProcess()

	Dim sVersion,sArch,sFile
	Dim sInstallName, sInstallerPath
	Dim sLogFolder,sLogName,sLogPath
	Dim sConfigDate,sConfigFile,sConfigCopyPath,sConfigEXEPath
	
	'// Apply Architecture arguments:
	'// If no argument provided check for MDT/SCCM variable
	'// If no variable or argument is provided, defualt to x86
	If oUtility.Arguments.Exists("arch") Then
		sArch = oUtility.Arguments("arch")
	ElseIf oEnvironment.Exists("Architecture") Then
		sArch = oEnvironment.Item("Architecture")
	Else
		sArch = "x86"
	End If

	'// Apply Config File arguments:
	'// If no argument provided check for MDT/SCCM variable
	If oUtility.Arguments.Exists("config") Then
		sConfigFile = oUtility.Arguments("config")
	ElseIf oEnvironment.Exists("DVConfigFile") Then
		sConfigFile = oEnvironment.Item("DVConfigFile")
	Else
		sConfigFile = oUtility.ScriptDir & "\Config\dvConfig_" & sConfigDate & ".txt"
	End If
	
	'// Variables: 
	'// Change if needed
	sVersion = "4.11.0.737"
	sFile = "DesktopValidator_" & sArch & "_" & sVersion & ".exe"
	
	If sArch = "x64" then
		sConfigEXEPath = oEnv("PROGRAMFILES") & "\Tumbleweed\Desktop Validator\dvconfig.exe"
	Else
		sConfigEXEPath =  oEnv("PROGRAMFILESX86") & "\Tumbleweed\Desktop Validator\dvconfig.exe"
	End If
	
	sConfigDate = "28JULY2016"
	sConfigCopyPath = oEnv("ALLUSERSPROFILE") & "\Tumbleweed"	
	
	sInstallName = "Tumbleweed Desktop Validator"
	sInstallerPath = oUtility.ScriptDir & "\Source\" & sVersion & "\" & sFile
	
	'// Build log path (incase not used in task sequence)
	If oEnvironment.Item("_SMSTSLogPath") = "" Then 
		sLogFolder = oEnv("TEMP") & "\AppLogs"
	Else
		sLogFolder = oEnvironment.Item("_SMSTSLogPath") & "\AppLogs"
	End If
	oUtility.VerifyPathExists sLogFolder
	sLogName = Replace(sInstallName & "_" & sVersion & "_" & sArch & ".log"," ","")
	sLogPath = sLogFolder & "\" & sLogName
	
	'// Start the process
	oLogging.CreateEntry "Starting " & sInstallName & " (" & sVersion & ") " & sArch & " installation", LogTypeInfo	
	
	If not oFSO.FileExists(sInstallerPath) then
		oLogging.CreateEntry sInstallerPath & " was not found, unable to install " & sInstallName & " (" & sVersion & ")", LogTypeError
		ZTIProcess = Failure
		Exit Function
	End if

	'// Disable Zone Checks
	oEnv("SEE_MASK_NOZONECHECKS") = 1 

	iRetVal = oUtility.RunWithHeartbeat("""" & sInstallerPath & """ /S /v""/qn /l*v """ & sLogPath & """""")
	
	if (iRetVal = 0) or (iRetVal = 3010) then
		ZTIProcess = Success 
		
		DeleteFile "C:\Users\Public\Desktop","TUmbleweed Desktop Validator.lnk"
		oUtility.VerifyPathExists sConfigCopyPath
		iRetVal = oUtility.RunWithHeartbeat("""" & sConfigEXEPath & """ -command write -file """ & sConfigFile & """")
		
		If sArch = "x64" then
			oShell.RegWrite "HKLM\SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{0768D7D7-0D13-4740-9684-A42CCF095BA4}\DisplayName", "Tumbleweed Desktop Validator (" & sConfigDate & ")", "REG_SZ"
		Else
			oShell.RegWrite "HKLM\SOFTWARE\WOW6432Node\Microsoft\Windows\CurrentVersion\Uninstall\{0768D7D7-0D13-4740-9684-A42CCF095BA4}\DisplayName", "Tumbleweed Desktop Validator (" & sConfigDate & ")", "REG_SZ"
		End If
		
		oLogging.CreateEntry "Finished " & sInstallName & " installation", LogTypeInfo
	Else 
		ZTIProcess = Failure
		oLogging.CreateEntry sInstallName & " installation failed with exit code = " & iRetVal, LogTypeError
	End If
	
	
	'// Enable Zone Checks
	oEnv.Remove("SEE_MASK_NOZONECHECKS")	

End Function 

Function DeleteFile(sPath, sName)

	On error resume next
	Dim strFullPath
	strFullPath = sPath & "\" & sName
	If oFSO.fileexists(strFullPath) Then
		oLogging.CreateEntry "File Found: " & strFullPath, LogTypeInfo
		oFSO.DeleteFile strFullPath,0
		oLogging.CreateEntry "File Deleted: " & strFullPath, LogTypeInfo
	Else
		oLogging.CreateEntry "File Not Found: " & strFullPath & ", skipping", LogTypeInfo
	End If

End Function
</script>
</job>

