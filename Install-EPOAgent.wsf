<job id="Install-EPOAgent">
<script language="VBScript" src="..\..\scripts\ZTIUtility.vbs"/>
<script language="VBScript"> 

' //----------------------------------------------------------------------------
' //
' // Solution: Richard's Deployment Script
' // File: Install-VLC.wsf
' //
' // Purpose: This will install McAfee EPO Agent Framework Package
' //
' // Author: Richard Tracy
' //
' // Usage: cscript Install-EPOAgent.wsf [/arch:x64|x86] [/FramePkgPath:<path>] [/prefix:<foldername>] [/debug:true]
' //
' //----------------------------------------------------------------------------

'//----------------------------------------------------------------------------
'// Global constant and variable declarations
'//---------------------------------------------------------------------------- 
Option Explicit 

Dim iRetVal 
'//----------------------------------------------------------------------------
'// Main routine
'//---------------------------------------------------------------------------- 
'On Error Resume Next
iRetVal = ZTIProcess
ProcessResults iRetVal
On Error Goto 0 

'//---------------------------------------------------------------------------
'// Function: ZTIProcess()
'//---------------------------------------------------------------------------
Function ZTIProcess() 

	Dim sVersion,sArch,sFile
	Dim sInstallName, sInstallerPath
	Dim sLogFolder,sLogName,sLogPath
	
	'// Apply Architecture arguments:
	'// If no argument provided check for MDT/SCCM variable
	'// If no variable or argument is provided, defualt to x86
	If oUtility.Arguments.Exists("arch") Then
		sArch = LCase(oUtility.Arguments("arch"))
	ElseIf oEnvironment.Exists("Architecture") Then
		sArch = LCase(oEnvironment.Item("Architecture"))
	Else
		sArch = "x86"
	End If
	
	'// Variables: 
	'// Change if needed
	sVersion = "5.6.0.878"
	
	sFile = "FramePkg.exe"
	
	If oUtility.Arguments.Exists("FramePkgPath") Then
		sInstallerPath = oUtility.Arguments("FramePkgPath") & "\" & sFile
	ElseIf oEnvironment.Exists("EPOFramePkgSource") Then
		sInstallerPath = oEnvironment.Item("EPOFramePkgSource") & "\" & sFile
	Else
		If oUtility.Arguments.Exists("Prefix") Then
			sInstallerPath = oUtility.ScriptDir & "\Source\" & oUtility.Arguments("Prefix") & "\" & sFile
		ElseIf oEnvironment.Exists("Prefix") Then
			sInstallerPath = oUtility.ScriptDir & "\Source\" & UCase(oEnvironment.Item("Prefix")) & "\" & sFile
		Else
			sInstallerPath = oUtility.ScriptDir & "\Source\" & sFile
		End if
	End if
	
	sInstallName = "EPO Agent Framework"
	
	'// Build log path (incase not used in task sequence)
	If oEnvironment.Exists("_SMSTSLogPath") Then 
		sLogFolder = oEnvironment.Item("_SMSTSLogPath") & "\AppLogs"
	Else
		sLogFolder = oEnv("TEMP") & "\AppLogs"
	End If
	oUtility.VerifyPathExists sLogFolder
	sLogName = Replace(sInstallName & "_" & sVersion & "_" & sArch & ".log"," ","")
	sLogPath = sLogFolder & "\" & sLogName
	
	'// Start the process
	oLogging.CreateEntry "Starting " & sInstallName & " (" & sVersion & ") " & sArch & " installation", LogTypeInfo	
	
	If not oFSO.FileExists(sInstallerPath) then
		oLogging.CreateEntry sInstallerPath & " was not found, unable to install " & sInstallName & " (" & sVersion & ")", LogTypeError
		ZTIProcess = Failure
		Exit Function
	End if
	
	'// Disable Zone Checks
	oEnv("SEE_MASK_NOZONECHECKS") = 1 
	
	iRetVal = oUtility.RunWithHeartbeat("""" & sInstallerPath & """ /s /INSTALL=Agent /enableVDIMode")
	'iRetVal = oUtility.RunWithHeartbeat("""" & sInstallerPath & """ /s /INSTALL=Agent")
	
	if (iRetVal = 0) or (iRetVal = 3010) then
		ZTIProcess = Success 
		oLogging.CreateEntry "Finished " & sInstallName & " installation", LogTypeInfo	
	Else 
		ZTIProcess = Failure
		oLogging.CreateEntry sInstallName & " installation failed with exit code = " & iRetVal, LogTypeError
	End If
	
	'// Enable Zone Checks
	oEnv.Remove("SEE_MASK_NOZONECHECKS")

End Function 

Function DeleteFile(sPath, sName)

	On error resume next
	Dim strFullPath
	strFullPath = sPath & "\" & sName
	If oFSO.fileexists(strFullPath) Then
		oLogging.CreateEntry "File Found: " & strFullPath, LogTypeInfo
		oFSO.DeleteFile strFullPath,0
		oLogging.CreateEntry "File Deleted: " & strFullPath, LogTypeInfo
	Else
		oLogging.CreateEntry "File Not Found: " & strFullPath & ", skipping", LogTypeInfo
	End If

End Function

</script>
</job>

















