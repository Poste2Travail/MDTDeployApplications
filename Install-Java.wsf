<job id="Install-Java">
<script language="VBScript" src="..\..\scripts\ZTIUtility.vbs"/>
<script language="VBScript"> 

' //----------------------------------------------------------------------------
' //
' // Solution: Richard's Deployment Script
' // File: Install-Java.wsf
' //
' // Purpose: This will install Java
' //
' // Author: Richard Tracy
' //
' // Usage: cscript Install-Java.wsf [/arch:x64|x86] [/debug:true]
' //
' //----------------------------------------------------------------------------

'//----------------------------------------------------------------------------
'// Global constant and variable declarations
'//---------------------------------------------------------------------------- 
Option Explicit 

Dim iRetVal

'//----------------------------------------------------------------------------
'// Main routine
'//---------------------------------------------------------------------------- 
'On Error Resume Next
iRetVal = ZTIProcess
ProcessResults iRetVal
On Error Goto 0

'//---------------------------------------------------------------------------
'// Function: ZTIProcess()
'//---------------------------------------------------------------------------
Function ZTIProcess() 

	Dim sVersion,sArch,sFile
	Dim sType
	Dim sInstallName, sInstallerPath
	Dim arySplitVer,sMajVer,sMinVer
	Dim sLogFolder,sLogName,sLogPath
	Dim sInstallerConfig
	Dim sPrefix
	
	'// Apply Architecture arguments:
	'If no argument provided check for MDT/SCCM variable
	'If no variable or argument is provided, defualt to x86
	If oUtility.Arguments.Exists("arch") Then
		sArch = oUtility.Arguments("arch")
	ElseIf oEnvironment.Exists("Architecture") Then
		sArch = oEnvironment.Item("Architecture")
	Else
		sArch = "x86"
	End If
	
	'// Variables: 
	'// Change if needed
	sVersion = "1.8.0.201"

	sType = "jre"

	arySplitVer = Split(sVersion, ".")
	sMajVer=arySplitVer(1)
	sMinVer=arySplitVer(3)
	
	' check if MDT variable exists
	If oEnvironment.Exists("Prefix") Then
		sPrefix = oEnvironment.Item("Prefix")
	End if
	
	If sArch = "x64" then
		sFile = sType & "-" & sMajVer & "u" & sMinVer & "-windows-x64.exe"
	Else
		sFile = sType & "-" & sMajVer & "u" & sMinVer & "-windows-i586.exe"
	End If

	sInstallName = "Java (" & UCase(sType) & ")"
	
	sInstallerPath = oUtility.ScriptDir & "\source\" & sVersion & "\" & sFile
	
	'// Build log path (incase not used in task sequence)
	If oEnvironment.Item("_SMSTSLogPath") = "" Then 
		sLogFolder = oEnv("TEMP") & "\AppLogs"
	Else
		sLogFolder = oEnvironment.Item("_SMSTSLogPath") & "\AppLogs"
	End If
	oUtility.VerifyPathExists sLogFolder
	sLogName = Replace(sInstallName & "_" & sVersion & "_" & sArch & ".log"," ","")
	sLogPath = sLogFolder & "\" & sLogName
	
	sInstallerConfig = oUtility.ScriptDir & "\Configs\java.installsettings.cfg"
	
	'// Start the process
	oLogging.CreateEntry "Starting " & sInstallName & " (" & sVersion & ") " & sArch & " installation", LogTypeInfo	
	
	If not oFSO.FileExists(sInstallerPath) then
		oLogging.CreateEntry sInstallerPath & " was not found, unable to install " & sInstallName & " (" & sVersion & ")", LogTypeError
		ZTIProcess = Failure
		Exit Function
	End if
	
	'// Disable Zone Checks
	oEnv("SEE_MASK_NOZONECHECKS") = 1 

	If not oFSO.FileExists(sInstallerConfig) then
		iRetVal = oUtility.RunWithHeartbeat("""" & sInstallerPath & """ /s REMOVEOUTOFDATEJRES=1 /L """ & sLogPath & """")
	Else
		oFSO.CopyFile sInstallerConfig, oEnv("TEMP") & "\", True
		iRetVal = oUtility.RunWithHeartbeat("""" & sInstallerPath & """ INSTALLCFG=""" & oEnv("TEMP") & "\java.settings.cfg"" /L """ & sLogPath & """")
	End if
	
	if (iRetVal = 0) or (iRetVal = 3010) then
		ZTIProcess = Success 
		oLogging.CreateEntry "Finished " & sInstallName & " installation", LogTypeInfo
		'DeleteFolder "C:\ProgramData\Microsoft\Windows\Start Menu\Programs\Java"
		oUtility.VerifyPathExists "C:\WINDOWS\Sun\Java\Deployment"
		
		'Copy deployment.config
		oLogging.CreateEntry "Copying " & oUtility.ScriptDir & "\Configs\deployment.config to C:\WINDOWS\Sun\Java\Deployment", LogTypeVerbose		
		oFSO.CopyFile oUtility.ScriptDir & "\Configs\deployment.config", "C:\WINDOWS\Sun\Java\Deployment\", True
		
		'Copy deployment.properties
		If not oFSO.FileExists(oUtility.ScriptDir & "\Configs\deployment_"& sPrefix & ".properties") then
			oFSO.CopyFile oUtility.ScriptDir & "\Configs\deployment.properties", "C:\WINDOWS\Sun\Java\Deployment\", True
		Else
			oFSO.CopyFile oUtility.ScriptDir & "\Configs\deployment_"& sPrefix & ".properties", "C:\WINDOWS\Sun\Java\Deployment\", True
		End If
		
		'Copy exception.sites
		If not oFSO.FileExists(oUtility.ScriptDir & "\Configs\exception_"& sPrefix & ".sites") then
			oFSO.CopyFile oUtility.ScriptDir & "\Configs\exception.sites", "C:\WINDOWS\Sun\Java\Deployment\", True
		Else
			oFSO.CopyFile oUtility.ScriptDir & "\Configs\exception_"& sPrefix & ".sites", "C:\WINDOWS\Sun\Java\Deployment\", True
		End If
	Else 
		ZTIProcess = Failure
		oLogging.CreateEntry sInstallName & " installation failed with exit code = " & iRetVal, LogTypeError
	End If

	'// Enable Zone Checks
	oEnv.Remove("SEE_MASK_NOZONECHECKS")

End Function 

Function CopyFile(sName,sDestPath)
	If oFSO.FileExists(oUtility.ScriptDir & "\" & sName) Then
		If Not oFSO.FolderExists(sDestPath) Then
			oLogging.CreateEntry "Created Directory: " & sDestPath, LogTypeInfo
			oFSO.CreateFolder sDestPath
		End If
		
		oLogging.CreateEntry "Preparing to copy " & oUtility.ScriptDir & "\" & sName, LogTypeInfo
		oFSO.CopyFile oUtility.ScriptDir  & "\" & sName, sDestPath, True
		oLogging.CreateEntry "The file " & sName & " was copied to " & sDestPath, LogTypeInfo
		CopyFile=0
	Else
		oLogging.CreateEntry "Failed to find : " & oUtility.ScriptDir  & "\" & sName, LogTypeERROR
		CopyFile=2
	End If

End Function

Function DeleteFolder(sPath)

	On error resume next
	Dim strPath
	strPath = sPath
	If oFSO.FolderExists(strPath) Then
		oLogging.CreateEntry "Folder Found: " & strPath, LogTypeInfo
		oFSO.DeleteFolder strPath,0
		oLogging.CreateEntry "Folder Deleted: " & strPath, LogTypeInfo
	Else
		oLogging.CreateEntry "Folder Not Found: " & strPath & ", skipping", LogTypeInfo
	End If

End Function

</script>
</job>



















