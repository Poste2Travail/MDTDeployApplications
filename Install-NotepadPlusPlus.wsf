<job id="Install-NotepadPlusPlus">
<script language="VBScript" src="..\..\scripts\ZTIUtility.vbs"/>
<script language="VBScript"> 

' //----------------------------------------------------------------------------
' //
' // Solution: Richard's Deployment Script
' // File: Install-NotepadPlusPlus.wsf
' //
' // Purpose: This will install Notepad++
' //
' // Author: Richard Tracy
' //
' // Usage: cscript Install-NotepadPlusPlus.wsf [/arch:x64|x86] [/debug:true]
' //
' //----------------------------------------------------------------------------

'//----------------------------------------------------------------------------
'// Global constant and variable declarations
'//---------------------------------------------------------------------------- 
Option Explicit 

Dim iRetVal

'//----------------------------------------------------------------------------
'// Main routine
'//---------------------------------------------------------------------------- 
'On Error Resume Next
iRetVal = ZTIProcess
ProcessResults iRetVal
On Error Goto 0

'//---------------------------------------------------------------------------
'// Function: ZTIProcess()
'//---------------------------------------------------------------------------
Function ZTIProcess() 

	Dim sInstallName,sVersion
	Dim sFile, sInstallerPath,sArch
	Dim sPluginsPath,sThemesPath
	Dim sLogFolder,sLogName,sLogPath
	
	'// Apply Architecture arguments:
	'If no argument provided check for MDT/SCCM variable
	'If no variable or argument is provided, defualt to x86
	If oUtility.Arguments.Exists("arch") Then
		sArch = LCase(oUtility.Arguments("arch"))
	ElseIf oEnvironment.Exists("Architecture") Then
		sArch = LCase(oEnvironment.Item("Architecture"))
	Else
		sArch = "x86"
	End If
	
	'// Variables: 
	'// Change if needed
	sVersion = "7.7.1"
	
	If sArch = "x64" then
		sFile = "npp." & sVersion & ".Installer.x64.exe"
		sPluginsPath = "C:\Program Files\Notepad++\plugins\ComparePlugin"
		sThemesPath = "C:\Program Files\Notepad++\themes"
	Else
		sFile = "npp." & sVersion & ".Installer.exe"
		sPluginsPath = "C:\Program Files (x86)\Notepad++\plugins\ComparePlugin"
		sThemesPath = "C:\Program Files (x86)\Notepad++\themes"
	End If
	
	sInstallName = "Notepad++"
	
	sInstallerPath = oUtility.ScriptDir & "\Source\" & sVersion & "\" & sFile
	
	'// Build log path (incase not used in task sequence)
	If oEnvironment.Exists("_SMSTSLogPath") Then 
		sLogFolder = oEnvironment.Item("_SMSTSLogPath") & "\AppLogs"
	Else
		sLogFolder = oEnv("TEMP") & "\AppLogs"
	End If
	oUtility.VerifyPathExists sLogFolder
	sLogName = Replace(sInstallName & "_" & sVersion & "_" & sArch & ".log"," ","")
	sLogPath = sLogFolder & "\" & sLogName
	
	'// Start the process
	oLogging.CreateEntry "Starting " & sInstallName & " (" & sVersion & ") " & sArch & " installation", LogTypeInfo	
	
	If not oFSO.FileExists(sInstallerPath) then
		oLogging.CreateEntry sInstallerPath & " was not found, unable to install " & sInstallName & " (" & sVersion & ")", LogTypeError
		ZTIProcess = Failure
		Exit Function
	End if
	
	'// Disable Zone Checks
	oEnv("SEE_MASK_NOZONECHECKS") = 1 

	iRetVal = oUtility.RunWithHeartbeat("""" & sInstallerPath & """ /S")
	
	if (iRetVal = 0) or (iRetVal = 3010) then
		ZTIProcess = Success
		If Not oFSO.FolderExists(sPluginsPath) Then
		  oFSO.CreateFolder sPluginsPath
		  oLogging.CreateEntry "Created folder" & sPluginsPath, LogTypeInfo
		End If
		oFSO.CopyFile ("" & oUtility.ScriptDir & "\Configs\" & sArch & "\ComparePlugin.dll"""), (sPluginsPath & "\ComparePlugin.dll"), True
		
		If Not oFSO.FolderExists(sThemesPath) Then
		  oFSO.CreateFolder sThemesPath
		  oLogging.CreateEntry "Created folder" & sThemesPath, LogTypeInfo
		End If
		oFSO.CopyFile ("" & oUtility.ScriptDir & "\Configs\themes\Solarized (Light).xml"""), (sPluginsPath & "\Solarized-Light.xml"), True
		oFSO.CopyFile ("" & oUtility.ScriptDir & "\Configs\themes\Solarized (Dark).xml"""), (sPluginsPath & "\Solarized.xml"), True
		
		oLogging.CreateEntry "Finished " & sInstallName & " installation", LogTypeInfo
	Else 
		ZTIProcess = Failure
		oLogging.CreateEntry sInstallName & " installation failed with exit code = " & iRetVal, LogTypeError
	End If
	
	'// Enable Zone Checks
	oEnv.Remove("SEE_MASK_NOZONECHECKS")

End Function 

Function CopyFile(sName,sDestPath)
	If oFSO.FileExists(oUtility.ScriptDir & "\" & sName) Then
		If Not oFSO.FolderExists(sDestPath) Then
			oLogging.CreateEntry "Created Directory: " & sDestPath, LogTypeInfo
			oFSO.CreateFolder sDestPath
		End If
		oLogging.CreateEntry "Preparing to copy " & oUtility.ScriptDir & "\" & sName, LogTypeInfo
		oFSO.CopyFile oUtility.ScriptDir  & "\" & sName, sDestPath, True
		oLogging.CreateEntry "The file " & sName & " was copied to " & sDestPath, LogTypeInfo
		CopyFile=0
	Else
		oLogging.CreateEntry "Failed to find : " & oUtility.ScriptDir  & "\" & sName, LogTypeERROR
		CopyFile=2
	End If

End Function

</script>
</job>






















