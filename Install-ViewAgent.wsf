<job id="Install-ViewAgent">
<script language="VBScript" src="..\..\scripts\ZTIUtility.vbs"/>
<script language="VBScript"> 

' //----------------------------------------------------------------------------
' //
' // Solution: Richard's Deployment Script
' // File: Install-ViewAgent.wsf
' //
' // Purpose: This will install Horizon View VDI Agent
' //
' // Author: Richard Tracy
' //
' // Usage: cscript Install-ViewAgent.wsf [/arch:x64|x86] [/debug:true]
' //
' //----------------------------------------------------------------------------

'//----------------------------------------------------------------------------
'// Global constant and variable declarations
'//---------------------------------------------------------------------------- 
Option Explicit 

Dim iRetVal

'//----------------------------------------------------------------------------
'// Main routine
'//---------------------------------------------------------------------------- 
'On Error Resume Next
iRetVal = ZTIProcess
ProcessResults iRetVal
On Error Goto 0

'//---------------------------------------------------------------------------
'// Function: ZTIProcess()
'//---------------------------------------------------------------------------
Function ZTIProcess() 

	Dim sVersion,sArch,sFile
	Dim sInstallName, sInstallerPath
	Dim sLogFolder,sLogName,sLogPath
	Dim sADDLOCALArgs
	
	'// Apply Architecture arguments:
	'If no argument provided check for MDT/SCCM variable
	'If no variable or argument is provided, defualt to x86
	If oUtility.Arguments.Exists("arch") Then
		sArch = LCase(oUtility.Arguments("arch"))
	ElseIf oEnvironment.Exists("Architecture") Then
		sArch = LCase(oEnvironment.Item("Architecture"))
	Else
		sArch = "x86"
	End If
	
	'// Variables: 
	'// Change if needed
	sVersion = "7.6.0-9539447"
	
	If sArch = "x64" then
		sFile = "VMware-Horizon-Agent-x86_64-" & sVersion & ".exe"
	Else
		sFile = "VMware-Horizon-Agent-x86-" & sVersion & ".exe"
	End If
	
	sInstallName = "VMWare Horizon View VDI Agent"

	sInstallerPath = oUtility.ScriptDir & "\Source\" & sFile
	
	'// Build log path (incase not used in task sequence)
	If oEnvironment.Exists("_SMSTSLogPath") Then 
		sLogFolder = oEnvironment.Item("_SMSTSLogPath") & "\AppLogs"
	Else
		sLogFolder = oEnv("TEMP") & "\AppLogs"
	End If
	oUtility.VerifyPathExists sLogFolder
	sLogName = Replace(sInstallName & "_" & sVersion & "_" & sArch & ".log"," ","")
	sLogPath = sLogFolder & "\" & sLogName
	
	'// Start the process
	oLogging.CreateEntry "Starting " & sInstallName & " (" & sVersion & ") " & sArch & " installation", LogTypeInfo	
	
	If not oFSO.FileExists(sInstallerPath) then
		oLogging.CreateEntry sInstallerPath & " was not found, unable to install " & sInstallName & " (" & sVersion & ")", LogTypeError
		ZTIProcess = Failure
		Exit Function
	End if
	
	'// Disable Zone Checks
	oEnv("SEE_MASK_NOZONECHECKS") = 1 
	

	sADDLOCALArgs = "Core,NGVC,RTAV,ThinPrint,FlashURLRedirection,SmartCard,USB,VmwVaudio,VmwVdisplay,VmVideo,FLASHMMR,HTML5MMR"
	'sADDLOCALArgs = "VmVideo,VmwVdisplay,NGVC,SmartCard,TSMMR,ThinPrint,VmwVaudio,RDP,BlastProtocol,Core,ClientDriveRedirection,FlashURLRedirection,RTAV,FLASHMMR,HTML5MMR"
	iRetVal = oUtility.RunWithHeartbeat("""" & sInstallerPath & """ /s /norestart /v""/qn VDM_VC_MANAGED_AGENT=1 VDM_FIPS_ENABLED=0 INSTALL_VDISPLAY_DRIVER=1 VDM_FLASH_URL_REDIRECTION=1 VDM_IP_PROTOCOL_USAGE=IPv4 ADDLOCAL=""" & sADDLOCALArgs & """ REBOOT=ReallySuppress /l*v """ & sLogPath & """""")
	
	if (iRetVal = 0) or (iRetVal = 3010) then
		ZTIProcess = Success 
		oLogging.CreateEntry "Finished " & sInstallName & " installation", LogTypeInfo
	Else 
		ZTIProcess = Failure
		oLogging.CreateEntry sInstallName & " installation failed with exit code = " & iRetVal, LogTypeError
	End If

	'// Enable Zone Checks
	oEnv.Remove("SEE_MASK_NOZONECHECKS")

End Function 

</script>
</job>















